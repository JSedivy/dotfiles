export PYENV_ROOT="$HOME/.pyenv"

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($(find $ZSH/**/*.zsh | grep -v backups | grep -v /lib/))

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

source $ZSH/lib/z/z.sh

source $ZSH/lib/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

eval "$(pyenv init -)"

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
